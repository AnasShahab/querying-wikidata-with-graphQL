\chapter{Evaluation of the generated SPARQL queries}

We now evaluate the SPARQL queries generated by each of the two approaches - GraphQL-LD and HyperGraphQL. This is done by using ten SPARQL queries taken from the official Wikidata documentation\footnote{https://www.wikidata.org/wiki/Wikidata:SPARQL\_query\_service/queries/examples.} and analysing whether these can also be generated using the two approaches. Any significant differences between the SPARQL queries are also compared. The queries used for evaluation are selected based on the different functionalities of SPARQL that can be used for fetching data from Wikidata.

In Wikidata we can use the specialized service with the URI <http://wikiba.se/ontology\#label> to fetch labels in specific languages. We cannot use this service in any of the approaches, and rdf:label\footnote{http://www.w3.org/2000/01/rdf-schema\#label.} is used instead. GraphQL-LD cannot filter to fetch labels in some particular languages. It retrieves labels in all the available languages for the Wikidata resource. On the other hand, HyperGraphQL can fetch labels but do filtering to fetch labels in any one language. 

HyperGraphQL translates fields into OPTIOANAL SPARQL triple patterns. So we would get more results than expected when writing GraphQL queries. Also, the use of a schema in HyperGraphQL restricts the object node item in a triple pattern to be an instance of some class. This restriction results in fewer exploration of data, as the object node could have been an instance of other classes too. This is evident in Query 8

Query 1 fetches at all items whose value of instance of is house cat.

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing34, caption={Query 1}]
SELECT ?item ?itemLabel
WHERE
{
    ?item wdt:P31 wd:Q146;.
    SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
\end{lstlisting}
\end{minipage}


\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing35, caption={Query 1 - GraphQL-LD}]
SELECT ?id ?id_id ?id_label WHERE {
  ?id_id <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q146>;
    <http://www.w3.org/2000/01/rdf-schema#label> ?id_label.
}
\end{lstlisting}
\end{minipage}


\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing36, caption={Query 1 - HypergraphQL}]
SELECT * WHERE {
  {
    SELECT ?x_1 WHERE {
      ?x_1 <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q146> .
    }
  }
  OPTIONAL {
    ?x_1 <http://www.w3.org/2000/01/rdf-schema#label> ?x_1_1 .
    FILTER (lang(?x_1_1) = "en") .
  }
}
\end{lstlisting}
\end{minipage}


Query 2 counts the total number of humans in Wikidata using COUNT function.

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing37, caption={Query 2}]
SELECT (COUNT(*) AS ?count)
WHERE {
  ?item wdt:P31 wd:Q5 .
}
\end{lstlisting}
\end{minipage}


\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing38, caption={Query 2 - GraphQL-LD}]
SELECT ?id ?id_totalCount WHERE {
  SELECT (COUNT(?id) AS ?id_totalCount) WHERE {
    ?id <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q5>.
  }
}
\end{lstlisting}
\end{minipage}

Query 2 cannot be impplemented with HyperGraphQL. HyperGraphQL does not support the use of functions. As a result, the query using the Count function cannot be implemented in HyperGraphQL.


Query 3 fetches Humans that do not have children using the "no value" handling, and considers only truthy values. 

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing39, caption={Query 3}]
SELECT ?human ?humanLabel
WHERE
{
  ?human wdt:P31 wd:Q5 
  ?human rdf:type wdno:P40 .  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" }
}
\end{lstlisting}
\end{minipage}

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing40, caption={Query 3 - GraphQL-LD}]
SELECT ?id ?id_id ?id_label WHERE {
  ?id_id <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q5>;
    <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> <http://www.wikidata.org/prop/novalue/P40>;
    <http://www.w3.org/2000/01/rdf-schema#label> ?id_label.
}
\end{lstlisting}
\end{minipage}

This query cannot be written in HyperGraphQL. The "no value" is represented as a class of an entity, statement or reference, and is defined to be of type owl:Class\footnote{http://www.w3.org/2002/07/owl\#Class.} Since we have modified the HyperGraphQL source code to use instance of property instead of rdf type in the schema, we cannot define "no value" correctly.


Query 4 in similar to Query 3 but it also considers non-truthy "no-value" statement to fetch Humans without children in Wikidata.

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing41, caption={Query 4}]
SELECT ?human ?humanLabel
WHERE
{
  ?human wdt:P31 wd:Q5 
  ?human p:P40 ?childStatement . 
  ?childStatement rdf:type wdno:P40 .
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . }
}
\end{lstlisting}
\end{minipage}


\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing42, caption={Query 4 - GraphQL-LD}]
SELECT ?id ?id_id ?id_childStatement ?id_label WHERE {
  ?id_id <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q5>;
    <http://www.wikidata.org/prop/P40> ?id_childStatement.
  ?id_childStatement <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> <http://www.wikidata.org/prop/novalue/P40>.
  ?id_id <http://www.w3.org/2000/01/rdf-schema#label> ?id_label.
}
\end{lstlisting}
\end{minipage}

Query 4 cannot be written in HyperGraphQL due to similar reasons as shown in Query 3.


Query 5 fetches the first 10 items in Wikidata that have the Wikimedia database name property.

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing43, caption={Query 5}]
SELECT ?item ?itemLabel ?value
WHERE
{
  ?item wdt:P1800 ?value
   SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
LIMIT 10
\end{lstlisting}
\end{minipage}

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing44, caption={Query 5 - GraphQL-LD}]
SELECT ?id ?id_wikimediaDatabase WHERE {
  SELECT ?id ?id_wikimediaDatabase WHERE { ?id <http://www.wikidata.org/prop/direct/P1800> ?id_wikimediaDatabase. }
  LIMIT 10
}
\end{lstlisting}
\end{minipage}

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing45, caption={Query 5 - HypergraphQL}]
SELECT * WHERE {
  {
    SELECT ?x_1 WHERE
    {
      ?x_1 <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q33120876> .
    } LIMIT 10
  }
  OPTIONAL {
     ?x_1 <http://www.w3.org/2000/01/rdf-schema#label> ?x_1_1 .
     FILTER (lang(?x_1_1) = "en") .
    }
    OPTIONAL {
      ?x_1 <http://www.wikidata.org/prop/direct/P1800> ?x_1_2 .
    }
}
\end{lstlisting}
\end{minipage}

Query 6 fetches mayors that are either a dog, a cat or a chicken by using the VALUES clause.

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing46, caption={Query 6}]
SELECT ?image ?speciesLabel ?mayorLabel ?placeLabel WHERE {
  VALUES ?species {wd:Q144 wd:Q146 wd:Q780}
  ?mayor wdt:P31 ?species .
  ?mayor p:P39 ?node .
  ?node ps:P39 wd:Q30185 .
  ?node pq:P642 ?place .
  OPTIONAL {?mayor wdt:P18 ?image}
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
\end{lstlisting}
\end{minipage}

Query 6 cannot be written in either GraphQL-LD or HyperGraphQL. GraphQL-LD does not support the use of VALUES clause. HyperGraphQL only supports the binding of subject variable in the root level to values. We have demonstrated this in Query 8.

Query 7 retrieves all the subclass of literary work in Wikidata.

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing47, caption={Query 7}]
SELECT ?s ?desc
WHERE
{
  ?s wdt:P279 wd:Q7725634 .
  OPTIONAL {
     ?s rdfs:label ?desc FILTER (lang(?desc) = "en").
   }
 }
\end{lstlisting}
\end{minipage}

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing48, caption={Query 7 - GraphQL-LD}]
SELECT ?id ?label WHERE {
  ?id <http://www.wikidata.org/prop/direct/P279> <http://www.wikidata.org/entity/Q7725634>.
  OPTIONAL { ?id <http://www.w3.org/2000/01/rdf-schema#label> ?label. }
}
\end{lstlisting}
\end{minipage}

Query 7 cannot be written in HyperGraphQL since values cannot be set to object nodes.

Query 8 fetches the destinations that can be reached from Antwerp International airport using the VALUES clause.

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing49, caption={Query 8}]
SELECT ?connectsairport ?connectsairportLabel ?place_served ?place_servedLabel ?coor
WHERE
{
  VALUES ?airport { wd:Q17480 }  
?airport wdt:P81 ?connectsairport ;
           wdt:P625 ?base_airport_coor .
  ?connectsairport wdt:P931 ?place_served ;
                   wdt:P625 ?coor .

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
\end{lstlisting}
\end{minipage}

Query 8 cannot be written in GraphQL-LD due to the same reasons provided in Query 6.

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing50, caption={Query 8 - HypergraphQL}]
SELECT * WHERE {
  VALUES ?x_1 { <http://www.wikidata.org/entity/Q17480> }
  ?x_1 <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q1248784> .
  OPTIONAL {
    ?x_1 <http://www.wikidata.org/prop/direct/P81> ?x_1_1 .
    ?x_1_1 <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q1248784> .
    OPTIONAL {
      ?x_1_1 <http://www.w3.org/2000/01/rdf-schema#label> ?x_1_1_1 .
      FILTER (lang(?x_1_1_1) = "en") .
    }
    OPTIONAL {
      ?x_1_1 <http://www.wikidata.org/prop/direct/P931> ?x_1_1_2 .
      ?x_1_1_2 <http://www.wikidata.org/prop/direct/P31> <http://www.wikidata.org/entity/Q515> .
      OPTIONAL {
        ?x_1_1_2 <http://www.w3.org/2000/01/rdf-schema#label> ?x_1_1_2_1 .
        FILTER (lang(?x_1_1_2_1) = "en") .
      }
      OPTIONAL {
        ?x_1_1_2 <http://www.wikidata.org/prop/direct/P625> ?x_1_1_2_2 .
      }
    }
  }
  OPTIONAL {
    ?x_1 <http://www.w3.org/2000/01/rdf-schema#label> ?x_1_2 .
    FILTER (lang(?x_1_2) = "en") .
  }
}
\end{lstlisting}
\end{minipage}

Query 9 fetches the locations of the works created by Pablo Picasso.

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing51, caption={Query 9}]
SELECT ?label ?coord ?subj
WHERE
{
   ?subj wdt:P170 wd:Q5593 .
  OPTIONAL {?subj wdt:P276 ?loc .
    ?loc wdt:P625 ?coord } .
   ?subj rdfs:label ?label FILTER (lang(?label) = "en")
}
\end{lstlisting}
\end{minipage}

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing52, caption={Query 9 - GraphQL-LD}]
SELECT ?id ?id_location_coordinateLocation ?id_label WHERE {
  ?id <http://www.wikidata.org/prop/direct/P170> <http://www.wikidata.org/entity/Q5593>;
    <http://www.w3.org/2000/01/rdf-schema#label> ?id_label.
  OPTIONAL {
    ?id <http://www.wikidata.org/prop/direct/P276> ?id_location.
    ?id_location <http://www.wikidata.org/prop/direct/P625> ?id_location_coordinateLocation.
  }
}
\end{lstlisting}
\end{minipage}

Query 9 cannot be written in HyperGraphQL owing to similar reasons mentioned in Query 7.

Query 10 uses VALUES to extract scientific articles of Lydia Pintscher.

\begin{minipage}{\linewidth}
\begin{lstlisting}[label=listing:listing53, caption={Query 10}]
SELECT ?entity ?entityLabel ?authorLabel WHERE {
  VALUES ?author {wd:Q18016466} #initialize "?author with the Wikidata item "Lydia Pintscher"
  ?entity wdt:P31 wd:Q13442814. #filter by scientific articles
  ?entity wdt:P50 ?author.
 SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
\end{lstlisting}
\end{minipage}

Query 10 cannot be written in either GraphQL-LD or HyperGraphQL for similar reasons as mentioned in Query 6.

\anas{Is an ending remark in evaluation needed}
\anas{Need to include GraphQL queries too in evaluation?}